Hello World Examples
1
###
# Don't forget to run with the .dat
# e.g. ./tape-device hello_world_examples.tape hello_world_examples.dat
###
.strings
ex_string=This is from a string in the .strings section:
greeting=Hello, World!
ex_mem=This is from data loaded into memory and read back out as chars:
ex_file=This is from data loaded from a file and read out as chars:
file_error=No input file, check note at top of basm file
ex_data=This is from data prepackaged in the tape file
.data
data_hello=["Hello from data"]
.ops
        fchk 0 prog
        prtln
        prts file_error
        prtln
        halt
        #Print string data example
prog:   prts ex_string     #print string named ex_string
        PRTLN              #newline
        prts greeting      #print string named greeting
        PRTLN              #newline

        #Letter values in ASCII
        const H 72
        const e 101
        const l 108
        const o 111
        const W 'W' #These letters
        const r 'r' #are converted to
        const d 'd' #their ASCII num
        const space 32

        #Prepare memory
        cpy acc H          #set ACC to const H (72)
        memw @0            #write value in ACC to memory address 0
        cpy acc e          #set ACC to const e
        memw @1            #write value in ACC to memory address 1
        cpy acc l          #set ACC to const l
        memw @2            #write value in ACC to memory address 2
        memw @3            #write value in ACC to memory address 3
        memw @9            #write value in ACC to memory address 9
        cpy acc o          #set ACC to const o
        memw @4            #write value in ACC to memory address 4
        memw @7            #write value in ACC to memory address 7
        cpy acc space      #set ACC to const space
        memw @5            #write value in ACC to memory address 5
        cpy acc W          #set ACC to const W
        memw @6            #write value in ACC to memory address 6
        cpy acc r          #set ACC to const r
        memw @8            #write value in ACC to memory address 8
        cpy acc d          #set ACC to const d
        memw @10           #write value in ACC to memory address 10

        #Print from memory
        prts ex_mem        #print string named ex_mem
        PRTLN              #newline
        cpy a1 @11         #set A1 to memory address 11
        cpy a0 @0          #set A0 to memory address 0
print:  memr a0            #read value from memory at address in A0 and set in ACC
        prtc acc           #print value in ACC as ASCII
        inc a0             #increment value in A0
        cmp a0 a1          #compare A0 and A1
        jne print          #if they were not equal jump to print
        PRTLN              #newline

        #Clear memory
        cpy a1 @11         #set A1 to memory address 11
        cpy a0 @0          #set A0 to memory address 0
        cpy acc 0          #set ACC to 0
clear:  memw a0            #write value in ACC to memory at address in A0
        inc A0             #increment A0
        cmp a0 a1          #compare A0 and A1
        jne clear          #if they were not equal jump to clear

        #Read from file
        #Program will crash if file not available
        prts ex_file       #print string named ex_file
        prtln              #newline
        fopen 0            #Open file
        cpy acc 10         #Set ACC to 10, this is used by filer as the max byte read count
        filer 0 @0         #Read bytes from file and save in memory at address in A0
        cpy a1 @10         #set A1 to memory address 11
        cpy a0 @0          #set A0 to memory address 0
fprint: memr a0            #read value from memory at address in A0 and set in ACC
        prtc acc           #print value in ACC as ASCII
        inc a0             #increment value in A0
        cmp a0 a1          #compare A0 and A1
        jne fprint         #if they were not equal jump to fprint
        PRTLN              #newline

        #Print from data
        prts ex_data          #print string named ex_data
        PRTLN              #newline
        ld a0 data_hello 0 1  #set A0 to array length of first array
        cpy acc a0            #load array length into acc
        ld a0 data_hello 1 0  #set A0 to first byte of first arryy
        prtd a0               #print ACC characters starting at data address in A0
        PRTLN              #newline
